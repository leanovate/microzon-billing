import org.apache.tools.ant.filters.ReplaceTokens

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.github.rholder:gradle-one-jar:1.0.3'
    }
}

apply plugin: 'java'
apply plugin: 'gradle-one-jar'
apply plugin: 'application'
apply plugin: 'eclipse'
apply plugin: 'idea'

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

group = "de.leanovate.microzon"
version =  '0.1.0'

repositories {
    mavenCentral()
}

dependencies {
    compile('io.dropwizard:dropwizard-core:0.7.1')
    compile('io.dropwizard:dropwizard-jdbi:0.7.1')
    compile('io.dropwizard:dropwizard-migrations:0.7.1')
    compile('org.apache.httpcomponents:httpclient:4.3.5')
    compile("mysql:mysql-connector-java:5.1.32")
    compile("com.github.kristofa:brave-impl:2.2.1") {
        exclude group: 'log4j', module: 'log4j'
        exclude group: 'org.slf4j', module: 'slf4j-log4j12'
    }
    compile("com.github.kristofa:brave-apache-http-interceptors:2.2.1") {
        exclude group: 'log4j', module: 'log4j'
        exclude group: 'org.slf4j', module: 'slf4j-log4j12'
    }
    compile("com.github.kristofa:brave-zipkin-spancollector:2.2.1") {
        exclude group: 'log4j', module: 'log4j'
        exclude group: 'org.slf4j', module: 'slf4j-log4j12'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.2.1'
}

task oneJar(type: OneJar) {
    mainClass = "de.leanovate.dose.billing.BillingApplication"
}

artifacts {
    oneJar
}

task dist(type:Copy) {
    def distDir = System.getenv("DIST_DIR") ?: "../vagrant/dists"

    dependsOn oneJar
    from oneJar.archivePath
    into distDir
}

task "dist-docker"(type:Copy) {
    def distDir = System.getenv("DIST_DIR") ?: "../docker/billing/dist"

    dependsOn oneJar
    from oneJar.archivePath
    into distDir
}

task debian(dependsOn: oneJar) << {
    copy {
        from 'src/main/debian'
        into 'build/deb/DEBIAN'
        filter(ReplaceTokens, tokens: [version: "$version-${System.env.TRAVIS_BUILD_NUMBER}".toString()])
    }
    copy {
        from oneJar.archivePath
        into 'build/deb/opt/billing'
        rename(/microzon-billing.*.jar/, 'billing.jar')
    }
    copy {
        from 'src/main/supervisor'
        into 'build/deb/etc/supervisor/conf.d'
    }
    exec {
        executable '/usr/bin/fakeroot'
        args '/usr/bin/dpkg-deb', "-b", "build/deb", 'build/microzon-billing.deb'
    }
    exec {
        executable '/usr/bin/curl'
        args "-X", "PUT", "-T", 'build/microzon-billing.deb', "-u${System.env.BINTRAY_USER}:${System.env.BINTRAY_KEY}", "https://api.bintray.com/content/untoldwind/deb/microzon/$version/pool/main/m/microzon/microzon-billing_$version-${System.env.TRAVIS_BUILD_NUMBER}_all.deb;deb_distribution=trusty;deb_component=main;deb_architecture=all?publish=1"
    }
}
